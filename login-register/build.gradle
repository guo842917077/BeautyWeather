def androidCompile = rootProject.ext.androidCompile
def isDebug = rootProject.ext.isDebug
def importLibrary = rootProject.ext.dependencies
def appId = rootProject.ext.appId
def enableBinding = rootProject.ext.enableDataBinding
def isPackage = rootProject.ext.isPackage;

if (isPackage) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}


android {
    compileSdkVersion androidCompile.compileSdkVersion

    defaultConfig {
        if (!isPackage) {
            applicationId appId.loginId
        }
        minSdkVersion androidCompile.minSdkVersion
        targetSdkVersion androidCompile.targetSdkVersion
        versionCode androidCompile.versionCode
        versionName androidCompile.versionName
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
        dataBinding {
            enabled enableBinding
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }

    buildTypes {
        debug {
            buildConfigField("boolean", "IsDebug", String.valueOf(isDebug))
        }

        release {
            buildConfigField("boolean", "IsDebug", String.valueOf(isDebug))
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            if (!isPackage) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude '**/debug/**'
                }
            }
        }
    }
}

dependencies {

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(":baselibrary")
    implementation project(":database-user")
    implementation project(":comm-config")

    implementation importLibrary["constraintLayout"]
    implementation importLibrary["androidxCompat"]
    implementation importLibrary["lifeRuntime"]
    implementation importLibrary["lifeExtensions"]
    implementation importLibrary["lifeCompiler"]
    implementation importLibrary["arouter-api"]
    annotationProcessor importLibrary["arouter-compiler"]
    implementation importLibrary["lottie"]
    implementation importLibrary["material-desgin"]

}